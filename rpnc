#!/usr/bin/perl

#   Sinn: Commandline-Pocketcalculator
#  Autor: t3o 2023++

# TODO the things on the stack should have their own classes (symbol, number). Not "element".
# TODO save/load vars
# TODO save/load list of commands in $command_queue -- oder wie machen, dass ein unterprogramm ausgefuehrt wird? Brauchen konkretes Beispiel!!

use strict;
use warnings;
use Data::Dumper; # the ultimate debugging tool
use Getopt::Long;
use YAML::Syck;
use FindBin;
use lib "$FindBin::Bin/lib";
use Parser;
use TheMachine;

use constant REVISION => "20231231-07";
use constant SAVEFILE_VERSION => 2;
$|=1;
$Data::Dumper::Sortkeys = 1;

GetOptions(
	'help|h' => \my $need_help,
	'nosave' => \my $nosave,         # default: false
	'noload' => \my $noload,         # default: false
	'statefile=s' => \my $statefile, # default: HOME/.rpnc
	'debug'  => \my $is_debugging,
	# 'quiet'  => \my $be_quiet,
);
usage() if $need_help;

print "Welcome to the wonderful world of R-P-N-C\n";
$statefile //= $ENV{"HOME"} . "/.rpnc";
my $machine = TheMachine->new();

if (-f $statefile && (! $noload))
{
	my $state = LoadFile( $statefile );
	my $version = $state->{_version} // "-1";
	if ($version != SAVEFILE_VERSION)
	{
		die("state file version mismatch. I found >$version< but I need >" . SAVEFILE_VERSION . "<");
	}

	$machine->{ defs  } = $state->{ defs  };
	$machine->{ flags } = $state->{ flags };
	if (defined( $state->{ stack } ))
	{
		foreach my $hr (@{ $state->{ stack } })
		{
			if ($hr->{type} eq "Number")
			{
				$machine->push( Number->new( $hr->{value} ));
			}
			if ($hr->{type} eq "String")
			{
				$machine->push( String->new( $hr->{value} ));
			}
		}
	}
}

my $pa = Parser->new();
my $loop = 434;
while ($loop)
{
	$machine->show();
	# Move the input function into TheMachine when it is programmable
	my $input = <STDIN>;
	unless (defined $input) # Ctrl-d
	{
		print "\n";
		last;
	}
	chomp($input);
	$input = "dup" unless length($input);
	$pa->set_stream( $input );

	while(4e4)
	{
		my $atom = $pa->next_atom();
		print " DEBUGGING atom from stream: " . Dumper($atom) if $is_debugging;
		# print "stream in pa: " . $pa->{stream} . "\n";
		last unless defined $atom;
		last if ref $atom eq "EndOfStream";
		if (ref $atom eq "Operator")
		{
			if (($atom->value() eq "q") || ($atom->value() eq "quit"))
			{
				$loop = 0;
				last;
			}
			$machine->do( $atom ); # do something
		}
		elsif (ref $atom eq "Number")
		{
			$machine->push( $atom );
		}
		elsif (ref $atom eq "String")
		{
			$machine->push( $atom );
		}
	}
}
$machine->shutdown();
unless ($nosave)
{
	my $state = {
		_version => SAVEFILE_VERSION,
		stack => [],
		defs  => $machine->{ defs  },
		flags => $machine->{ flags },
		revision => REVISION,
		comment => "statefile of the RPNC rpn calculator",
	};
	foreach my $elem ( @{ $machine->{ stack } } )
	{
		unshift( @{ $state->{ stack } }, {type => ref $elem, value => $elem->{value}} );
	}
	DumpFile( $statefile, $state );
}

# --------------------------------------------------------
exit(0);

# ----------------------------------------------------------------------------
sub usage
{
	my $msg = shift;
	print "$0 REVISION " . REVISION . "\n";
	print "ERROR: $msg\n" if defined $msg;
	print "Usage: $0 ....\n";
	exit(1);
}

