#!/usr/bin/perl

#   Sinn: Commandline-Pocketcalculator
#  Autor: t3o 2023++

# DONE change the parser to read char by char in a mode like "number" or "string"
# DONE we need types of things on the stack
# TODO inv
# TODO ^
# TODO do we need 3 over (ovr) ? or "<n> copy"?
# DONE: Parser ändern, dass er zeichenweise liest, dabei in einem Modus wie zb "zahl" ist, oder "string"
# DONE brauchen (eigentlich) Typen für Dinge auf dem Stack (das koennte ein Package sein)

# TODO: Programmierfähigkeit -- unter einem Namen eine zu parsende Zeichenkette ablegen
# TODO: Hilfe einbauen
# TODO konstanten
#

use strict;
use warnings;
use Data::Dumper; # the ultimate debugging tool
use Getopt::Long;
use YAML::Syck;
use Test::More;
use Test::Exception;
use FindBin;
use lib "$FindBin::Bin/lib";
use Parser;
use TheMachine;

use constant REVISION => "20231213";
use constant SAVEFILE_VERSION => 2;
$|=1;
$Data::Dumper::Sortkeys = 1;

GetOptions(
	'help|h' => \my $need_help,
	'nosave' => \my $nosave,         # default: false
	'noload' => \my $noload,         # default: false
	'statefile=s' => \my $statefile, # default: HOME/.rpnc
	'debug'  => \my $is_debugging,
	# 'quiet'  => \my $be_quiet,
);
usage() if $need_help;

print "Welcome to the wunderful world of R-P-N-C\n";
$statefile //= $ENV{"HOME"} . "/.rpnc";
my $rpnc = TheMachine->new();

if (-f $statefile && (! $noload))
{
	my $state = LoadFile( $statefile );
	my $version = $state->{_version} // "-1";
	if ($version != SAVEFILE_VERSION)
	{
		die("state file version mismatch. I found >$version< but I need >" . SAVEFILE_VERSION . "<");
	}

	$rpnc->{ vars  } = $state->{ vars  };
	$rpnc->{ flags } = $state->{ flags };
	if (defined( $state->{ stack } ))
	{
		foreach my $hr (@{ $state->{ stack } })
		{
			$rpnc->push( Element->new( $hr->{type}, $hr->{value} ));
		}
	}
}

my $ops = $rpnc->operators();
push(@$ops, 'q');
push(@$ops, 'help');
push(@$ops, '?');
push(@$ops, 'copy');
my $pa = Parser->new( operators => $ops );
my $loop = 434;
while ($loop)
{
	$rpnc->show();
	my $input = <STDIN>;
	unless (defined $input) # Ctrl-d
	{
		print "\n";
		last;
	}
	chomp($input);
	$pa->set_stream( $input );

	my $atom;
	while(434)
	{
		my $atom = $pa->next_atom();
		print " DEBUGGING atom from stream: " . Dumper($atom) if $is_debugging;
		print "stream in pa: " . $pa->{stream} . "\n";
		last unless defined $atom;
		last if $atom->is_end();
		if ($atom->is_quit()) # first check things to handle here:
		{
			$loop = 0;
			last;
		}
		elsif ($atom->is_error())
		{
			$rpnc->error( $atom->value() );
		}
		elsif ($atom->is_help())
		{
		      print "TODO: There will be some help in the future.\n";
		}
		else { # handle everything else in the machine:
			$rpnc->do( $atom ); # handle NUMBER OPERATOR
		}
	}
}
$rpnc->shutdown();
unless ($nosave)
{
	my $state = {
		_version => SAVEFILE_VERSION,
		stack => [],
		vars  => $rpnc->{ vars  },
		flags => $rpnc->{ flags },
		revision => REVISION,
		comment => "statefile of the RPNC rpn calculator",
	};
	foreach my $elem ( @{ $rpnc->{ stack } } )
	{
		unshift( @{ $state->{ stack } }, {type => $elem->{type}, value => $elem->{value}} );
	}
	DumpFile( $statefile, $state );
}

# --------------------------------------------------------
exit(0);

# ----------------------------------------------------------------------------
sub usage
{
	my $msg = shift;
	print "$0 REVISION " . REVISION . "\n";
	print "ERROR: $msg\n" if defined $msg;
	print "Usage: $0 ....\n";
	exit(1);
}

